apply plugin: "com.jfrog.artifactory"
apply plugin: 'maven-publish'

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifactoryPublish.dependsOn('build', 'generatePomFileForAarPublication')

publishing {
    publications {
        aar(MavenPublication) {
            groupId = "in.swiggy.android"
            artifactId = "connectionclass"
            version = "1.0.1"
            artifact("$buildDir/outputs/aar/connectionclass-release.aar")
            artifact androidSourcesJar
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                configurations.implementation.allDependencies.each { dependency ->
                    if (dependency.group != null && dependency.name != null && dependency.version != null) {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', dependency.group)
                        dependencyNode.appendNode('artifactId', dependency.name)
                        dependencyNode.appendNode('version', dependency.version)
                    }
                }
            }
        }
    }
}

artifactory {
    contextUrl = "${artifactory_contextUrl}"
    publish {
        repository {
            repoKey = "${artifactory_repokey}"
            username = "${artifactory_user}"
            password = "${artifactory_password}"
        }
        defaults {
            publications('aar')
            publishArtifacts = true
            publishPom = true
        }
    }
}